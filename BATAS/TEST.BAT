IF %MODE%==CLEAN GOTO :EOF

CALL ISREG16 %1
IF %RESULT%==1 GOTO :REG16
CALL ISREG8 %1
IF %RESULT%==1 GOTO :REG8
CALL ISMRM16 %1
IF %RESULT%==1 GOTO :MRM
ECHO Expected register or ModR/M (IP=%IP%)

:REG16
IF x%1x%2==xAXxWORD GOTO :AXIMM
IF x%2==xWORD GOTO :MRM2I16
CALL ISMRM16 %2
IF %RESULT%==1 GOTO :R16MRM
ECHO Expected immediate or ModR/M in second operand (IP=%IP%)

:AXIMM
CALL RPN! _%IP%_3+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL EMITBYTE 169
CALL EMITWORD %4
GOTO :EOF

:R16MRM
CALL CMRM16 %2
CALL RPN! _%IP%_2+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :NOCHECK1
CALL IS8SIGN %3
IF %RESULT%==1 GOTO :NOCHECK1
ECHO Byte displacement truncated (IP=%IP%)
:NOCHECK1
CALL EMITBYTE 133
CALL CREG16 %1
CALL RPN! _%MRM%_%REG%_8*+
CALL EMITBYTE %RESULT%
IF %DISPBYTES%==1 CALL EMITBYTE %3
IF %DISPBYTES%==2 CALL EMITWORD %3
GOTO :EOF

:REG8
IF x%1x%2==xALxBYTE GOTO :ALIMM
IF x%2==xBYTE GOTO :MRM2I8
CALL ISMRM8 %2
IF %RESULT%==1 GOTO :R8MRM
ECHO Expected immediate or ModR/M in second operand (IP=%IP%)

:ALIMM
CALL RPN! _%IP%_2+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL IS8SIGN %3
IF %RESULT%==1 GOTO :NOCHECK2
ECHO Byte immediate truncated (IP=%IP%)
:NOCHECK2
CALL EMITBYTE 168
CALL EMITBYTE %3
GOTO :EOF

:R8MRM
CALL CMRM8 %2
CALL RPN! _%IP%_2+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :NOCHECK3
CALL IS8SIGN %3
IF %RESULT%==1 GOTO :NOCHECK3
ECHO Byte displacement truncated (IP=%IP%)
:NOCHECK3
CALL EMITBYTE 132 
CALL CREG8 %1
CALL RPN! _%MRM%_%REG%_8*+
CALL EMITBYTE %RESULT%
IF %DISPBYTES%==1 CALL EMITBYTE %3
IF %DISPBYTES%==2 CALL EMITWORD %3
GOTO :EOF

:MRM
CALL CMRM16 %1
IF %DISPBYTES%==0 GOTO :MRM2
GOTO :MRM3

:MRM2
IF x%2==xWORD GOTO :MRM2I16
IF x%2==xBYTE GOTO :MRM2I8
ECHO Expected byte or word immediate in second operand (IP=%IP%)

:MRM3
IF x%3==xWORD GOTO :MRM3I16
IF x%3==xBYTE GOTO :MRM3I8
ECHO Expected byte or word immediate in second operand (IP=%IP%)

:MRM2I8
CALL CMRM8 %1
CALL RPN! _%IP%_3+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL IS8SIGN %3
IF %RESULT%==1 GOTO :NOCHECK6
ECHO Byte immediate truncated (IP=%IP%)
:NOCHECK6
CALL EMITBYTE 246
CALL EMITBYTE %MRM%
CALL EMITBYTE %3
GOTO :EOF

:MRM3I8
CALL CMRM8 %1
CALL RPN! _%IP%_3+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :NOCHECK8
CALL IS8SIGN %2
IF %RESULT%==1 GOTO :NOCHECK8
ECHO Byte displacement truncated (IP=%IP%)
:NOCHECK8
CALL IS8SIGN %4
IF %RESULT%==1 GOTO :NOCHECK9
ECHO Byte immediate truncated (IP=%IP%)
:NOCHECK9
CALL EMITBYTE 246
CALL EMITBYTE %MRM%
IF %DISPBYTES%==1 CALL EMITBYTE %2
IF %DISPBYTES%==2 CALL EMITWORD %2
CALL EMITBYTE %4
GOTO :EOF

:MRM2I16
CALL CMRM16 %1
CALL RPN! _%IP%_4+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL EMITBYTE 247
CALL EMITBYTE %MRM%
CALL EMITWORD %3
GOTO :EOF

:MRM3I16
CALL RPN! _%IP%_4+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :NOCHECKC
CALL IS8SIGN %2
IF %RESULT%==1 GOTO :NOCHECKC
ECHO Byte displacement truncated (IP=%IP%)
:NOCHECKC
CALL EMITBYTE 247
CALL EMITBYTE %MRM%
IF %DISPBYTES%==1 CALL EMITBYTE %2
IF %DISPBYTES%==2 CALL EMITWORD %2
CALL EMITWORD %4
GOTO :EOF

:EOF
